rule compress_vcf:
    conda:
        pipeline_path + "envs/htslib.yml"
    singularity:
        "docker://quay.io/biocontainers/htslib:1.9--ha228f0b_7"   
    input:
        "{any_vcf}.{vcf_or_bed}",
    output:
        temp("{any_vcf}.{vcf_or_bed, (vcf|bed)}.gz"),
    shell:
        """
        bgzip -c {input[0]} > {output[0]}
        """

rule index_vcf_bed:
    singularity:
        "docker://quay.io/biocontainers/htslib:1.9--ha228f0b_7"  
    conda:
        pipeline_path + "envs/htslib.yml"
    input:
        "{any_vcf}.{vcf_or_bed}.gz",
    output:
        temp("{any_vcf}.{vcf_or_bed, (vcf|bed)}.gz.tbi"),
    shell:
        """
        tabix -f -p {wildcards.vcf_or_bed} {input[0]}
        """
        
rule sort_vcf:
    conda:
        pipeline_path + "envs/bcftools.yml"
    singularity:
        "docker://quay.io/biocontainers/bcftools:1.9--h68d8f2e_7"  
    input:
        "{any_vcf}.vcf.gz",
    output:
        temp("{any_vcf}_sorted.vcf"),
    shell:
        """
        bcftools sort {input[0]}  > {output[0]}
        """
